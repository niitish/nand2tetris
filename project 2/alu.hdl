// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx and nx
    Mux16(a=x, b[0..15]=false, sel=zx, out=xa);
    Not16(in=xa, out=xb);
    Mux16(a=xa, b=xb, sel=nx, out=xc);

    // zy and ny
    Mux16(a=y, b[0..15]=false, sel=zy, out=ya);
    Not16(in=ya, out=yb);
    Mux16(a=ya, b=yb, sel=ny, out=yc);

    // f
    And16(a=xc, b=yc, out=xay);
    Add16(a=xc, b=yc, out=xoy);
    Mux16(a=xay, b=xoy, sel=f, out=o);
    
    // no
    Not16(in=o, out=od);
    Mux16(a=o, b=od, sel=no, out=of);
    
    // ng
    And16(a[0..15]=true, b=of, out[15]=ng);

    // or16way isn't predefined, using mux to split of in two 8 bit parts
    Mux16(a=of, b=of, sel=true, out[0..7]=b1, out[8..15]=b2);
    Or8Way(in=b1, out=or1);
    Or8Way(in=b2, out=or2);
    Or(a=or1, b=or2, out=zrr);
    Not(in=zrr, out=zr);

    Or16(a=of, b[0..15]=false, out=out);
}